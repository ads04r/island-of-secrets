  10 REM ISLAND OF SECRETS -- requires at least a 48K machine

  15 LET wv=70
  20 GO SUB 2820
  30 LET d=r: IF r=20 THEN LET d=FN r(80)
  40 GO SUB 650: CLS : PRINT "ISLAND OF SECRETS", "TIME REMAINING:";  l
  50 PRINT g$;  TAB (0);  "STRENGTH = ";  INT (y);  TAB (18);  "WISDOM = ";  x: PRINT g$
  55 LET li=VAL (a$(1))
  60 PRINT "YOU ARE ";  (i$(li))( TO h(li));  " "; : GO SUB 720: LET n=0
  70 FOR i=1 TO c4
  80 LET c=0: READ y$
  90 IF l(i)=r AND f(i)<1 THEN LET c=1
 100 IF n>0 AND c=1 THEN LET a$=a$+","
 110 IF c=1 THEN LET a$=a$+" "+y$: LET n=n+1
 120 NEXT i
 130 IF n>0 THEN LET a$="*YOU SEE"+a$: GO SUB 720
 140 PRINT : PRINT g$;  f$
 150 PRINT : PRINT "WHAT WILL YOU DO"; 
 160 INPUT e$: PRINT "  ";  e$
 170 LET c$="": LET x$="": LET a=0: LET o=52: LET li=LEN (e$)
 180 FOR i=1 TO li-1
 190 IF e$(i)=" "AND c$=""THEN LET c$=e$( TO i-1)
 200 IF e$(i+1)<>" "AND c$>""THEN LET x$=e$(i+1 TO ): LET i=li-1
 210 NEXT i
 220 IF x$=""THEN LET c$=e$
 230 IF LEN (c$)<3 THEN LET c$=c$+"???"
 240 FOR i=1 TO v
 250 IF c$( TO 3)=v$(3*(i-1)+1 TO 3*(i-1)+3) THEN LET a=i
 260 NEXT i
 270 GO SUB 760
 280 LET b$="": IF a=0 THEN LET a=v+1
 290 IF x$="???"THEN LET f$="MOST ACTIONS NEED TWO WORDS"
 300 IF a>v OR o=52 THEN LET f$=w$+c$+" "+x$
 310 IF a>v AND o=52 THEN LET f$="WHAT!"
 320 LET l=l-1: LET y=FN s(z)
 330 LET b$=STR$(o)+STR$(l(o))+STR$(f(o))+STR$(r)
 340 GO SUB g(a)
 350 IF r=61 THEN LET x=x-FN r(2)+1
 360 IF r=14 AND FN r(3)=1 THEN LET y=y-1: LET f$="YOU ARE BITTEN"
 370 IF f(36)<1 AND -r<>f(22) THEN LET f(36)=f(36)+1: LET l(36)=r: LET y=y-1
 380 IF r<>l(16) AND l(16)>0 THEN LET l(16)=1+FN r(4)
 390 IF r<>l(39) THEN LET l(39)=10*(FN r(5)-1)+7+FN r(3)
 400 IF r=l(39) AND r<>l(43) AND f(13)>-1 THEN LET y=y-2: LET x=x-2
 410 IF r<78 THEN LET l(32)=76+FN r(2)
 420 IF r=33 OR r=57 OR r=73 AND FN r(2)=1 THEN LET l(25)=r
 430 IF r=l(32) AND FN r(2)=1 AND f(32)=0 THEN GO SUB 1310
 440 IF r=19 AND y<70 AND f(43)=0 AND FN r(4)=1 THEN LET f$="PUSHED INTO THE PIT": LET f(w)=1
 450 IF r<>l(41) THEN LET l(41)=21+(FN r(3)*10)+FN r(2)
 460 IF r=l(41) THEN LET f(41)=f(41)-1: IF f(41)<-4 THEN GO SUB 1230
 470 IF f(43)=0 THEN LET l(43)=r
 480 IF l(43)<18 AND r<>9 AND r<>10 AND f(w-2)<1 THEN GO SUB 1330
 490 IF r=18 THEN LET y=y-1
 500 IF y<50 THEN LET o=FN r(9): GO SUB 1530: IF l(o)=r THEN LET f$="YOU DROP SOMETHING"
 510 IF l<900 AND r=23 AND f(36)>0 AND FN r(3)=3 THEN GO SUB 1360
 520 IF r=47 AND f(8)>0 THEN LET f$=f$+" YOU CAN GO NO FURTHER"
 530 IF f(8)+f(11)+f(13)=-3 THEN LET f(w)=1: GO SUB 2800
 540 IF f(w)=0 AND l>0 AND y>1 AND x>1 THEN GO TO 30
 550 IF l<1 OR y<1 THEN LET f$="YOU HAVE FAILED, THE EVIL ONE SUCCEEDS"
 560 PRINT : PRINT f$: PRINT "YOUR FINAL SCORE=";  INT (x+y+(ABS (l/7*(l<640))))
 570 PRINT : PRINT : PRINT "GAME OVER"
 580 STOP 
 640 RETURN 


 650 LET d=d*10+lr : REM 1>=d>=80
 660 RESTORE d : REM anything from 2870 to 3660 ie the location strings
 670 READ a$
 680 LET d$=a$(LEN(a$)-3 TO ): LET a$=a$( TO LEN(a$)-4)
 690 IF r=39 THEN LET rd=FN r(5): LET d$="101110100"(rd TO rd+3)
 700 IF r=20 THEN LET d$="1110"
 710 GO SUB 2780: RETURN 


 720 FOR i=2 TO LEN (a$): LET e$=a$(i)
 730 PRINT e$; : IF e$=" "AND PEEK (23688)<z THEN PRINT 
 740 NEXT i
 750 PRINT ". "; : LET a$="": RETURN 


 760 IF LEN (x$)<3 THEN LET x$=x$+"???"
 770 FOR i=1 TO w
 780 IF x$( TO 3)=z$(3*(i-1)+1 TO 3*(i-1)+3) THEN LET o=i
 790 NEXT i: IF o=0 THEN LET o=52
 800 RETURN 


 810 LET d=0: LET c=0: IF o=52 THEN LET d=a
 820 IF o>c4 AND o<w THEN LET d=o-c4
 830 IF b$="500012"OR b$="500053"OR b$="500045"THEN LET d=4
 840 IF b$="500070"OR b$="500037"OR b$="510011"OR b$="510041"THEN LET d=1
 850 IF b$="510043"OR b$="490066"OR b$="490051"THEN LET d=1
 860 IF b$="510060"OR b$="480056"THEN LET d=2
 870 IF b$="510044"OR b$="510052"THEN LET d=3
 880 IF b$="490051"AND f(29)=0 THEN GO SUB 2110: RETURN 
 890 IF r=l(39) AND (x+y<180 OR r=10) THEN LET f$=w$+"LEAVE!": RETURN 
 900 IF r=l(32) AND f(32)<1 AND d=3 THEN LET f$="HE WILL NOT LET YOU PAST": RETURN 
 910 IF r=47 AND f(44)=0 THEN LET f$="THE ROCKS MOVE TO PREVENT YOU": RETURN 
 920 IF r=28 AND f(7)<>1 THEN LET f$="THE ARMS HOLD YOU FAST": RETURN 
 930 IF r=45 AND f(40)=0 AND d=4 THEN LET f$="HISSSS!": RETURN 
 940 IF r=25 AND f(16)+l(16)<>-1 AND d=3 THEN LET f$="TOO STEEP TO CLIMB": RETURN 
 950 IF r=51 AND d=3 THEN LET f$="THE DOOR IS BARRED!": RETURN 
 955 IF d=5 THEN GO TO 970
 960 IF d>0 THEN IF d$(d)="0"THEN LET r=r+VAL ("-10+10+01-01"(d*3-2 TO d*3)): LET c=1
 970 LET f$="OK"
 980 IF d<1 OR c=0 THEN LET f$=w$+"GO THAT WAY"
 990 IF r=33 AND l(16)=0 THEN LET l(16)=FN r(4): LET f(16)=0: LET f$="THE BEAST RUNS AWAY!"
1000 IF r<>l(25) OR o<>25 THEN RETURN 
1010 LET f$="": LET a$="#YOU BOARD THE CRAFT "
1020 IF x<60 THEN LET a$=a$+s$
1030 LET a$=a$+t$
1040 GO SUB 2740: FOR d=1 TO wv*2: NEXT d
1050 IF x<60 THEN LET a$="#TO SERVE OMEGAN FOREVER!": LET f(w)=1
1060 IF x>59 THEN LET a$="#THE BOAT SKIMS THE DARK SILENT WATERS": LET r=57
1070 GO SUB 2750: FOR d=1 TO wv*2: NEXT d: RETURN 


1080 IF ((f(o)>0 AND f(o)<9) OR l(o)<>r) AND o<=c3 THEN LET f$="WHAT "+x$+"?": RETURN 
1090 IF b$="3450050"THEN LET y=y-8: LET x=x-5: LET f$="THEY ARE CURSED": RETURN 
1100 IF b$="3810010"THEN GO SUB 1370
1110 IF (a=15 AND o<>20 AND o<>1) OR (a=29 AND o<>16) OR o>c3 THEN LET f$=w$+c$+" "+x$: RETURN 
1120 IF l(o)=r AND (f(o)<1 OR f(o)=9) AND o<c3 THEN LET l(o)=0: LET a=-1
1130 IF o=16 AND l(10)<>0 THEN LET l(o)=r: LET f$="IT ESCAPED": LET a=0
1140 IF o>c1 AND o<c2 THEN LET f=f+2: LET a=-1
1150 IF o>=c2 AND o<=c3 THEN LET g=g+2: LET a=-1
1160 IF o>c1 AND o<c3 THEN LET l(o)=-81
1170 IF a=-1 THEN LET f$="TAKEN": LET x=x+4: LET e=e+1: IF f(o)>1 THEN LET f(o)=0
1180 IF b$<>"246046"OR l(11)=0 THEN RETURN 
1190 LET f$=u$: LET l(o)=r: IF FN r(3)<3 THEN RETURN 
1200 LET a$="#"+u$+r$
1210 LET r=63+FN r(6): LET l(16)=1: LET f$=""
1220 GO SUB 2740: RETURN 


1230 CLS : LET f$="": LET a$="#THE LOGMEN "+m$
1240 LET f(41)=0: LET y=y-4: LET x=x-4
1250 IF r<34 THEN LET a$=a$+"THROW YOU IN THE WATER": LET r=32
1260 IF r>33 THEN LET a$=a$+"TIE YOU UP IN A STOREROOM": LET r=51
1270 GO SUB 2750: FOR d=1 TO wv: NEXT d
1280 FOR i=3 TO 4
1290 IF l(i)=0 THEN LET l(i)=42
1300 NEXT i: RETURN 


1310 LET a$="*THE SWAMPMAN TELLS HIS TALE"
1320 GO SUB 2740: LET f(32)=-1: RETURN 


1330 LET f$="MEDIAN CAN DISABLE THE EQUIPMENT"
1340 IF l(8)=0 THEN LET f$=f$+" AND ASKS YOU FOR THE PEBBLE YOU CARRY"
1350 RETURN 


1360 LET f(36)=-(FN r(4)+6): LET f$="A STORM BREAKS OVERHEAD!": RETURN 


1370 FOR k=1 TO 30: CLS : PRINT "////LIGHTNING FLASHES!": NEXT k
1380 LET l(39)=r: LET y=y-8: LET x=x-2: RETURN 


1390 IF (o<>24 AND l(o)>0) OR o=52 THEN LET f$="YOU DON'T HAVE THE "+x$: RETURN 
1400 PRINT "GIVE THE "; x$; " TO WHOM"; : INPUT x$
1410 LET q=o: GO SUB 760: LET n=o: LET o=q
1420 IF r<>l(n) THEN LET f$="THE "+x$+" IS NOT HERE": RETURN 
1430 IF b$="10045"AND n=40 THEN LET l(o)=81: LET f(40)=1: LET f$="THE SNAKE UNCURLS"
1440 IF b$="2413075"AND n=30 AND g>1 THEN LET f(11)=0: LET f$="HE OFFERS HIS STAFF": LET g=g-1
1450 LET b$=b$( TO 3): LET f$="IT IS REFUSED"
1460 IF b$="300"AND n=42 THEN LET x=x+10: LET l(o)=81
1470 IF b$="120"AND n=42 THEN LET x=x+10: LET l(o)=81
1480 IF b$="40-"AND n=32 THEN LET f(n)=1: LET l(o)=81
1490 IF b$( TO 2)="80"AND n=43 THEN LET l(o)=81: GO SUB 1560
1500 IF l(o)=81 OR (o=24 AND l(11)>0 AND g>0) THEN LET f$="IT IS ACCEPTED"
1510 IF n=41 THEN LET l(o)=51: LET f$="IT IS TAKEN"
1520 RETURN 


1530 IF o=4 AND l(o)=0 THEN LET l(o)=81: LET x=x-1: LET f$="IT BREAKS!": RETURN 
1540 IF l(o)=0 AND o<=c1 THEN LET l(o)=r: LET f$="DONE": LET e=e-1
1550 RETURN 


1560 LET a$="*HE TAKES IT ": IF r<>8 THEN LET a$=a$+"RUNS DOWN THE CORRIDOR,"
1570 GO SUB 2740: LET a$="*AND CASTS IT INTO THE CHEMICAL VATS, PURIFYING THEM WITH"
1580 LET a$=a$+" A CLEAR BLUE LIGHT REACHING FAR INTO THE LAKES AND RIVERS BEYOND"
1590 LET f(8)=-1: GO SUB 2750: FOR d=1 TO wv*2: NEXT d: RETURN 


1600 IF l(i)<>0 AND i<c1 THEN LET i=i+1: GO TO 1600
1610 IF l(i)=0 THEN LET l(i)=r: LET f(i)=0: GO SUB 1540: LET f$="YOU DROP SOMETHING"
1620 RETURN 


1630 IF (o<c1 OR o>c3) AND x$<>"???"THEN LET f$=w$+c$+" "+x$: LET x=x-1: RETURN 
1640 LET f$="YOU HAVE NO FOOD": IF f>0 THEN LET f=f-1: LET y=y+10: LET f$="OK"
1650 IF o=3 THEN LET x=x-5: LET y=y-2: LET f$="THEY MAKE YOU VERY ILL!"
1660 RETURN 


1670 IF o=31 THEN GO SUB 2380: RETURN 
1680 IF x$<>"???"AND (o<21 OR o>c3) THEN LET f$=w$+c$+" "+x$: LET x=x-1: RETURN 
1690 LET f$="YOU HAVE NO DRINK": IF g>0 THEN LET g=g-1: LET y=y+7: LET f$="OK"
1700 RETURN 


1710 IF b$( TO 4)="1600"THEN LET f(o)=-1: LET f$="IT ALLOWS YOU TO RIDE"
1720 RETURN 


1730 IF b$="2644044"THEN LET f$="CHEST OPEN": LET f(6)=9: LET f(5)=9: LET f(15)=9
1740 IF b$="2951151"THEN LET f$="THE TRAPDOOR CREAKS": LET f(29)=0: LET x=x+3
1750 RETURN 


1760 LET y=y-2: IF b$="3577077"AND l(9)=0 THEN LET f(23)=0: LET l(23)=r
1770 IF v>15 AND v<19 AND (l(9)=0 OR l(15)=0) THEN LET f$="OK"
1780 IF b$="1258158"OR b$="2758158"AND l(15)=0 THEN LET f(12)=0: LET f(27)=0: LET f$="CRACK!"
1790 IF b$( TO 4)="1100"AND r=10 THEN GO SUB 1980
1800 IF a=18 AND (o>29 AND o<34) OR (o>38 AND o<44) OR o=16 THEN GO SUB 1900
1810 RETURN 


1820 LET y=y-2: LET x=x-2: IF r<>l(o) AND l(o)<>0 THEN RETURN 
1830 IF o=39 THEN LET f$="HE LAUGHS DANGEROUSLY"
1840 IF o=32 THEN LET f$="THE SWAMPMAN IS UNMOVED"
1850 IF o=33 THEN LET f$=w$+"TOUCH HER!": LET l(3)=81
1860 IF o=41 THEN LET f$="THEY THINK THAT'S FUNNY!"
1870 IF r=46 THEN GO SUB 1200
1880 IF b$( TO 4)="1400"AND r=l(39) THEN GO SUB 1980
1890 LET y=y-8: LET x=x-5: RETURN 


1900 IF l(9)>0 THEN RETURN 
1910 LET y=y-12: LET x=x-10: LET f$="THAT WOULD BE UNWISE!"
1920 IF r<>l(o) THEN RETURN 
1930 LET f(w)=1: LET a$="#A THUNDER SPLITS THE SKY!": LET f$=""
1940 LET a$=a$+"IT IS THE TRIUMPHANT VOICE OF OMEGAN.": GO SUB 2740
1950 LET a$="#WELL DONE ALPHAN! THE MEANS BECOME THE END.."
1960 LET a$=a$+"I CLAIM YOU AS MY OWN! HA HA HAH!": GO SUB 2750
1970 FOR d=1 TO wv: NEXT d: LET x=0: LET l=0: LET y=0: RETURN 


1980 CLS : GO SUB 2010*((o-10)=1)+2060*((o-10)>1)
1990 LET x=x+10: LET l(o)=81: LET f(o)=-1: GO SUB 720: FOR d=1 TO wv*2: NEXT d
2000 RETURN 


2010 LET a$="#IT SHATTERS RELEASING A DAZZLING RAINBOW OF COLOURS!"
2020 IF l(2)<>r THEN RETURN 
2030 LET a$=a$+"THE EGG HATCHES INTO A BABY DAKTYL "+o$
2040 LET l(39)=81: LET l(2)=81: LET f(2)=-1: LET y=y+40
2050 RETURN 


2060 IF l(13)<>r THEN RETURN 
2070 LET a$="*THE COAL BURNS WITH A WARM RED FLAME": LET f(13)=-1
2080 IF r=10 AND r=l(39) THEN LET a$=a$+" WHICH DISSOLVES OMEGAN'S CLOAK": LET y=y+20
2090 RETURN 


2100 IF r<>51 OR f(29)>0 THEN LET f$=w$+c$+" HERE": LET x=x+1
2110 LET x=x-1: LET r=FN r(5): CLS : PRINT "SWIMMING IN THE POISONOUS WATERS"
2120 LET j=0: LET b$="": LET f$="YOU SURFACE": PRINT "YOUR STRENGTH = ";  INT (y)
2130 FOR i=1 TO r
2140 IF y<15 THEN PRINT "YOU ARE VERY WEAK"
2150 PRINT "WHICH WAY "; : INPUT x$: PRINT x$: LET x$=x$(1): LET b$=b$+x$: NEXT i
2160 FOR i=1 TO r
2170 LET y=FN s(z)-3: IF b$(i)="N"THEN LET j=j+1
2180 NEXT i: IF r/2>j AND y>1 THEN GO TO 2110
2190 IF y<2 THEN LET f$="YOU GOT LOST AND DROWNED"
2200 LET r=30+FN r(3): RETURN 


2210 IF f(36)>-1 THEN RETURN 
2220 CLS : PRINT "YOU CAN RUN TO SHELTER IN:": PRINT "1) GRANDPA'S SHACK"
2230 PRINT "2) CAVE OF SNELM": PRINT "3) LOG CABIN": PRINT "CHOOSE FROM 1-3": INPUT a$
2240 IF a$>"0"AND a$<"4"THEN LET r=CODE ("A >"(VAL (a$)))-21: LET f(22)=-r
2250 PRINT "YOU RUN BLINDLY THROUGH THE STORM": LET f$="YOU REACH SHELTER"
2260 FOR d=1 TO wv: NEXT d: RETURN 


2270 IF b$="3075075"OR b$="3371071"THEN LET f$="HOW WILL YOU DO THAT"
2280 IF b$="3371071"AND a=28 THEN LET f(3)=0: LET f$="SHE NODS SLOWLY": LET x=x+5
2290 RETURN 


2300 LET f$="EXAMINE THE BOOK FOR CLUES"
2310 IF b$( TO 3)="600"THEN LET f$=l$
2320 RETURN 


2330 IF b$="40041"THEN LET f(4)=-1: LET f$="FILLED"
2340 RETURN 


2350 LET f$=x$: IF x$=h$ AND r=47 AND f(8)=0 THEN LET f(44)=1: LET f$=j$
2360 IF x$<>p$ OR r<>l(42) OR l(3)<81 OR l(12)<81 THEN RETURN 
2370 LET f$="HE EATS THE FLOWERS - AND CHANGES": LET f(42)=1: LET f(43)=0: RETURN 


2380 IF f(4)+l(4)<>-1 THEN LET f$="YOU DON'T HAVE "+x$: RETURN 
2390 CLS : PRINT "YOU TASTE A DROP AND..": FOR d=1 TO wv: NEXT d: LET f$="*OUCH!": LET y=y-4: LET x=x-7
2400 CLS : FOR i=1 TO ABS (f(36))+3
2410 LET l=l-1: IF y<100 OR -r=f(22) THEN LET y=y+1
2420 PRINT "TIME PASSES": FOR d=1 TO wv: NEXT d
2430 NEXT i
2440 IF l>100 OR f(36)<1 THEN LET x=x+2: LET f(36)=1
2450 IF a=37 OR a=36 THEN LET f$="OK"
2460 RETURN 


2470 IF r=l(25) THEN LET f$="THE BOATMAN WAVES BACK"
2480 IF b$( TO 3)="700"THEN LET f(7)=1: LET f$=n$: LET x=x+8
2490 RETURN 


2500 LET f$="A-DUB-DUB": IF b$( TO 4)<>"2815"THEN RETURN 
2510 IF f(o)=1 THEN LET f(o)=0: LET f$=k$: RETURN 
2520 IF l(5)=0 THEN LET f(8)=0: GO SUB 1080: LET f$="THE STONE UTTERS "+h$
2530 RETURN 


2540 CLS : PRINT " INFO - ITEMS CARRIED": GO SUB 2780
2550 PRINT g$;  TAB (0);  " FOOD="; f; TAB (23); "DRINK="; g: PRINT g$; : LET f$="OK"
2560 FOR i=1 TO c4
2570 READ y$: IF l(i)=0 THEN PRINT y$
2580 NEXT i
2590 PRINT g$; : GO SUB 2730: RETURN 


2600 LET c$="LOAD": IF a=41 THEN LET c$="SAVE"
2610 PRINT "PREPARE TO "; c$: GO SUB 2730
2640 IF a=41 THEN LET f(50)=r: LET f(49)=y: LET f(48)=x: LET f(47)=f: LET f(46)=g: LET f(45)=l
2660 IF a=40 THEN LOAD "ISDATA"DATA l(): LOAD "ISDATA"DATA f()
2670 IF a=41 THEN SAVE "ISDATA"DATA l(): SAVE "ISDATA"DATA f()
2700 IF a=40 THEN LET r=f(50): LET y=f(49): LET x=f(48): LET f=f(47): LET g=f(46): LET l=f(45)
2710 LET f$="OK": RETURN 


2720 LET f(w)=-1: LET f$="YOU RELINQUISH YOUR QUEST.": LET l=1: RETURN 


2730 INPUT "PRESS RETURN";  a$: RETURN 


2740 CLS 
2750 GO SUB 720: FOR d=1 TO wv: NEXT d: RETURN 


2780 RESTORE lr+810: REM 2860+810=3670, start of item strings
2790 RETURN 


2800 LET a$="*THE WORLD LIVES WITH NEW HOPE!": GO SUB 2750
2810 LET f$="YOUR QUEST IS OVER": RETURN 


2820 PRINT "INITIALISING"
2830 LET lr=2860: LET z=8
2840 LET v=42: LET w=51: LET c4=43
2850 DIM i$(7,7)
2860 DIM l(52): DIM f(52): DIM h(7): DIM g(43)
2870 DATA "4THE FURTHEST DEPTHS OF THE FOREST1001"
2880 DATA "4THE DEPTHS OF THE MUTANT FOREST1000"
2890 DATA "7A PATH OUT OF THE OVERGROWN DEPTHS1000"
2900 DATA "6A CARNIVOROUS TREE1000"
2910 DATA "4A CORRAL BENEATH THE CRIMSON CANYON1110"
2920 DATA "7THE TOP OF A STEEP CLIFF1011"
2930 DATA "4THE MARSH FACTORY1001"
2940 DATA "4THE SLUDGE FERMENTATION VATS1110"
2950 DATA "7THE UPPERMOST BATTLEMENTS1001"
2960 DATA "4OMEGAN'S SANCTUM1110"
2970 DATA "4SNELM'S LAIR0001"
2980 DATA "2A DARK CAVE0000"
2990 DATA "1BROKEN BRANCHES0100"
3000 DATA "1A THICKET OF BITING BUSHES0000"
3010 DATA "1A HUGE GLASS STONE1110"
3020 DATA "7THE EDGE OF CRIMSON CANYON0011"
3030 DATA "4THE CLONE FACTORY0101"
3040 DATA "4A CORRIDOR OF CLONE STORAGE CASKS1100"
3050 DATA "7EDGE OF THE WELL0000"
3060 DATA "4THE ROOM OF SECRET VISIONS1110"
3070 DATA "4SNELM'S INNER CHAMBER0111"
3080 DATA "3THE SOUTHERN EDGE OF THE FOREST0101"
3090 DATA "7A LEAFY PATH1000"
3100 DATA "3A FORK IN THE PATH0100"
3110 DATA "7AN APPARENTLY UNCLIMBABLE ROCKY PATH1100"
3120 DATA "7A LEDGE ATOP THE CRIMSON CANYON0010"
3130 DATA "4A TALL ENTRANCE CHAMBER1101"
3140 DATA "4A LOW PASSAGE WITH ARMS REACHING FROM THE WALLS1010"
3150 DATA "7THE APPROACH TO THE WELL OF DESPAIR0001"
3160 DATA "4A DIM CORRIDOR DEEP IN THE CASTLE1010"
3170 DATA "4THE STAGNANT WATERS OF THE CRAWLING CREEK1001"
3180 DATA "4A SHALLOW POOL OFF THE CREEK1100"
3190 DATA "7A LOG PIER, JUTTING OUT OVER THE CREEK0000"
3200 DATA "4A STRETCH OF FEATURELESS DUNES1100"
3210 DATA "1A GROUP OF TALL TREES1010"
3220 DATA "7A NARROW LEDGE AT THE SUMMIT OF THE CANYON0011"
3230 DATA "2A MONSTEROUS PORTAL IN THE CASTLE WALL0011"
3240 DATA "4A CHAMBER INCHES DEEP WITH DUST0001"
3250 DATA "4HERE1111"
3260 DATA "2A CARVED ARCHWAY0010"
3270 DATA "4A SMALL HUT IN THE LOG SETTLEMENT0111"
3280 DATA "1A HUGE SPLIT-LOG TABLE1001"
3290 DATA "4THE PORCH OF THE LOGMEN'S CABIN0110"
3300 DATA "4GRANDPA'S SHACK1101"
3310 DATA "3A CLEARING IN THE TREES BY A RICKETY SHACK0010"
3320 DATA "4THE NEST OF A HUGE DACTYL0111"
3330 DATA "6THE CASTLE OF DARK SECRETS BY TWO HUGE STONES0011"
3340 DATA "4A ROOM LITTERED WITH BONES0111"
3350 DATA "4THE CELL OF WHISPERED SECRETS0111"
3360 DATA "4THE LIBRARY OF WRITTEN SECRETS0111"
3370 DATA "4A REFUSE STREWN STOREROOM1111"
3380 DATA "4THE LOGMEN'S HALL0000"
3390 DATA "5A LOG BUILDING1000"
3400 DATA "7A RUTTED HILLSIDE1100"
3410 DATA "7A WINDSWEPT PLAIN AMONGST STONE MEGALITHS0100"
3420 DATA "7THE STEPS OF AN ANCIENT PYRAMID1010"
3430 DATA "7THE ISLAND OF SECRETS0111"
3440 DATA "1A BROKEN MARBLE COLUMN1001"
3450 DATA "7AN EXPANSE OF CRACKED, BAKED EARTH1100"
3460 DATA "4A DESERTED ADOBE HUT1010"
3470 DATA "4A LIVID GROWTH OF MAD ORCHIDS1011"
3480 DATA "4A CORNER STREWN WITH BROKEN CHAIRS0111"
3490 DATA "7THE BRIDGE NEAR TO A LOG SETTLEMENT0011"
3500 DATA "1A CRUMBLING MASS OF PETRIFIED TREES1011"
3510 DATA "3THE EDGE OF THE PYRAMID1101"
3520 DATA "7THE ROOF OF THE ANCIENT PYRAMID0100"
3530 DATA "3AN IMPASSABLE SPLIT IN THE PYRAMID1110"
3540 DATA "7A BARREN BLASTED WASTELAND0001"
3550 DATA "4AN EXPANSE OF BLEAK, BURNT LAND1100"
3560 DATA "5A DELAPIDATED ADOBE HUT0110"
3570 DATA "4THE HEART OF THE LILIES0101"
3580 DATA "4THE MIDST OF THE LILIES1100"
3590 DATA "3A RIVER'S EDGE BY A LOG BRIDGE0100"
3600 DATA "3A PETRIFIED VILLAGE BY A RIVER CROWDED WITH LILIES0100"
3610 DATA "4THE REMAINS OF A VILLAGE1100"
3620 DATA "3THE ENTRANCE TO A PETRIFIED VILLAGE1100"
3630 DATA "4A SWAMP MATTED WITH FIBROUS ROOTS1100"
3640 DATA "2A VILLAGE OF HOLLOW STUMPS DEFYING THE SWAMP0100"
3650 DATA "4A TUNNEL INTO ONE OF THE TREE STUMPS1100"
3660 DATA "4A HOLLOW CHAMBER MANY METRES IN DIAMETER1110"
3670 DATA "A SHINY APPLE"
3680 DATA "A FOSSILISED EGG"
3690 DATA "A LILY FLOWER"
3700 DATA "AN EARTHENWARE JUG"
3710 DATA "A DIRTY OLD RAG"
3720 DATA "A RAGGED PARCHMENT"
3730 DATA "A FLICKERING TORCH"
3740 DATA "A GLISTENING PEBBLE"
3750 DATA "A WOODMAN'S AXE"
3760 DATA "A COIL OF ROPE"
3770 DATA "A RUGGED STAFF"
3780 DATA "A CHIP OF MARBLE"
3790 DATA "A POLISHED COAL"
3800 DATA "A PIECE OF FLINT"
3810 DATA "A GEOLOGIST'S HAMMER"
3820 DATA "A WILD CANYON BEAST"
3830 DATA "A GRAIN LOAF"
3840 DATA "A JUICY MELON"
3850 DATA "SOME BISCUITS"
3860 DATA "A GROWTH OF MUSHROOMS"
3870 DATA "A BOTTLE OF WATER"
3880 DATA "A FLAGON OF WINE"
3890 DATA "A FLOWING SAP"
3900 DATA "A SPARKLING FRESHWATER SPRING"
3910 DATA "THE BOATMAN"
3920 DATA "A STRAPPED OAK CHEST"
3930 DATA "A FRACTURE IN THE COLUMN"
3940 DATA "A MOUTH-LIKE OPENING"
3950 DATA "AN OPEN TRAPDOOR"
3960 DATA "A PARCHED, DESSICATED VILLAGER"
3970 DATA "A STILL OF BUBBLING GREEN LIQOUR"
3980 DATA "A TOUGH SKINNED SWAMPMAN"
3990 DATA "THE SAGE OF THE LILIES"
4000 DATA "WALL AFTER WALL OF EVIL BOOKS"
4010 DATA "A NUMBER OF SOFTER ROOTS"
4020 DATA "FIERCE LIVING STORM THAT FOLLOWS YOU"
4030 DATA "MALEVOLENT WRAITHS WHO PUSH YOU TOWARD THE WELL"
4040 DATA "HIS DREADED CLOAK OF ENTROPY"
4050 DATA "OMEGAN THE EVIL ONE"
4060 DATA "AN IMMENSE SNAKE WOUND AROUND THE HUT"
4070 DATA "A GROUP OF AGGRESSIVE LOGMEN"
4080 DATA "THE ANCIENT SCAVENGER", "MEDIAN"
4090 GO SUB 2780
4100 DATA "BY", "FACING", "AT", "IN", "OUTSIDE", "BENEATH", "ON"
4105 DATA 2,6,2,2,7,7,2
4110 RESTORE lr+1230: REM 2860+1230 = 4090
4120 FOR i=1 TO 7: READ i$(i): NEXT i
4125 FOR i=1 TO 7: READ h(i): NEXT i
4130 LET r=23: LET b=8: LET l=1000: LET e=0
4140 LET c1=16: LET c2=21: LET c3=24: LET f=2: LET g=2
4150 LET f=2: LET g=2
4160 LET y=100: LET x=35
4170 LET h$="MNgIL5;/U^kZpcL%LJ\5LJm-ALZ/SkIngRm73**MJFF          "
4180 LET q$="90101191001109109000901000111000000100000010000000000"
4190 LET g$="--------------------------------"
4200 LET f$="LET YOUR QUEST BEGIN"
4210 LET k$="REFLECTIONS STIR WITHIN"
4220 LET l$="REMEMBER ALADDIN IT WORKED FOR HIM"
4230 LET m$="DECIDE TO HAVE A LITTLE FUN AND "
4240 LET n$="THE TORCH BRIGHTENS"
4250 LET u$="YOU ANGER THE BIRD"
4260 LET w$="YOU CAN'T "
4270 LET p$="REMEMBER OLD TIMES"
4280 LET r$=" WHICH FLIES YOU TO A REMOTE PLACE"
4290 LET s$="FALLING UNDER THE SPELL OF THE BOATMAN "
4310 LET t$="AND ARE TAKEN TO THE ISLAND OF SECRETS"
4320 LET j$="THE STONES ARE FIXED"
4330 LET o$="WHICH TAKES OMEGAN IN ITS CLAWS AND FLIES AWAY"
4340 LET v$="N??S??E??W??GO?GETTAKGIVDROLEAEATDRIRIDOPEPICCHOCHITAPBREFIGSTRATT"
4350 LET v$=v$+"HITKILSWISHEHELSCRCATRUBPOLREAEXAFILSAYWAIRESWAVINFXLOXSAQUI"
4360 LET z$="APPEGGFLOJUGRAGPARTORPEBAXEROPSTACHICOAFLIHAMCANLOAMELBISMUS"
4370 LET z$=z$+"BOTWINSAPWATBOACHECOLSTOTRAVILLIQSWASAGBOOROOASAWRACLOOMESNA"
4380 LET z$=z$+"LOGSCAMEDNORSOUEASWESUP?DOWIN?OUT???"
4390 FOR i=1 TO w+1
4400 LET l(i)=CODE (h$(i))-32: LET f(i)=CODE (q$(i))-48
4410 NEXT i
4420 LET h$="STONY WORDS"
4430 DEF FN r(z)=INT (RND*z+1)
4440 FOR i=1 TO 43: READ g(i): NEXT i
4450 DEF FN s(z)=y-(e/c4+.1)
4460 RETURN 


4470 DATA 810,810,810,810,810,1080,1080,1390,1530
4480 DATA 1540,1630,1670,1710,1730,1080,1760,1760,1760,1760
4490 DATA 1820,1820,1820,1820,1910,2100,2210,2270,2270,1080
4500 DATA 2500,2500,2300,2300,2330,2350,2400,2400,2470,2540
4510 DATA 2600,2600,2720,640

